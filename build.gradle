plugins {
    id 'java'
    id 'eclipse'
    id 'maven-publish'
    id 'net.minecraftforge.gradle' version '[6.0,6.2)'
    id 'com.diffplug.eclipse.apt' version '4.3.0'
    id 'org.spongepowered.mixin' version '0.7.+'
}

version = '0.2.0'
group = 'com.tlregen'

base {
    archivesName = 'tlregen'
}

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

println "Java: ${System.getProperty 'java.version'}, JVM: ${System.getProperty 'java.vm.version'} (${System.getProperty 'java.vendor'}), Arch: ${System.getProperty 'os.arch'}"
minecraft {
    mappings channel: 'official', version: '1.20.1'
    copyIdeResources = true
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        configureEach {
            workingDirectory project.file('run')
            //property 'forge.logging.markers', 'SCAN'
            property 'forge.logging.console.level', 'debug'

            mods {
                tlregen {
                    source sourceSets.main
                }
            }
        }

        client {
            property 'forge.enabledGameTestNamespaces', 'tlregen'
        }

        server {
            property 'forge.enabledGameTestNamespaces', 'tlregen'
            args '--nogui'
        }

        gameTestServer {
            property 'forge.enabledGameTestNamespaces', 'tlregen'
        }

        data {
            args '--mod', 'tlregen', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
        }
    }
}

sourceSets.main.resources { 
	srcDir 'src/generated/resources' 
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.20.1-47.3.7'
    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
}

java {
    withSourcesJar()
    withJavadocJar()
}

mixin {
	add sourceSets.main, 'tlregen.mixins.refmap.json'
	config 'tlregen.mixins.json'
	debug.verbose = true
    debug.export = true
}

tasks.named('jar', Jar).configure {
    manifest {
        attributes([
            "Specification-Title": "TLReGen",
            "Specification-Vendor": "tlregenapi",
            "Specification-Version": "1",
            "Implementation-Title": project.name,
            "Implementation-Version": "${version}",
            "Implementation-Vendor" :"tlregenapi",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
	//The following line should be commented out in dev, but uncommented when making a jar
    //finalizedBy 'reobfJar'
}

artifacts {
	archives javadocJar
	archives sourcesJar
}

publishing {
    publications {
        TLReGen(MavenPublication) {
            artifact jar
            artifact sourcesJar
            artifact javadocJar
        }
    }
    repositories {
        maven {
            name = "Local"
            url = uri(file("${project.projectDir}/../../Maven"))
        }
        //maven { 
        //    name = "Remote"
        //    url "file:///${project.projectDir}/maven2" 
        //}
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}